set(JOYNR_ENABLE_DLT_LOGGING @JOYNR_ENABLE_DLT_LOGGING@)

include(CMakeFindDependencyMacro)
include(FindPkgConfig)

find_dependency(Boost @JOYNR_BOOST_REQUIRED_VERSION@ REQUIRED
                COMPONENTS system filesystem thread)
find_dependency(spdlog @JOYNR_SPDLOG_REQUIRED_VERSION@ REQUIRED)
if(${JOYNR_ENABLE_DLT_LOGGING})
    pkg_check_modules(DLT REQUIRED IMPORTED_TARGET automotive-dlt)
endif()
# websocketpp-config.cmake iw not guarding target addition; workaround: check it here.
if(NOT TARGET websocketpp::websocketpp)
    find_dependency(websocketpp @JOYNR_WEBSOCKETPP_REQUIRED_VERSION@ REQUIRED)
endif()
find_dependency(MoCOCrW @JOYNR_MOCOCRW_REQUIRED_VERSION@ REQUIRED)
find_dependency(muesli @JOYNR_MUESLI_REQUIRED_VERSION@ REQUIRED)
# Keep this as last, it interferes with finding Boost components.
find_dependency(smrf @JOYNR_SMRF_REQUIRED_VERSION@ REQUIRED)

@PACKAGE_INIT@

if(NOT TARGET Joynr::JoynrLib)
    get_filename_component(CURRENT_CMAKE_DIR "${CMAKE_CURRENT_LIST_FILE}" PATH)
    include("${CURRENT_CMAKE_DIR}/JoynrTargets.cmake")

    # Backward compatibility
    get_target_property(
        Joynr_LIB_COMMON_INCLUDE_DIRS
        Joynr::JoynrLib
        INTERFACE_INCLUDE_DIRECTORIES
    )
    get_target_property(
        Joynr_LIB_COMMON_LIBRARIES
        Joynr::JoynrLib
        INTERFACE_LINK_LIBRARIES
    )
    get_target_property(
        Joynr_LIB_WS_INCLUDE_DIRS
        Joynr::JoynrWsRuntime
        INTERFACE_INCLUDE_DIRECTORIES
    )
    get_target_property(
        Joynr_LIB_WS_LIBRARIES
        Joynr::JoynrWsRuntime
        INTERFACE_LINK_LIBRARIES
    )
endif()

set(Joynr_BIN_DIR "@PACKAGE_JOYNR_INSTALL_BINDIR@")
set(Joynr_RESOURCES_DIR "@PACKAGE_JoynrConfig_RESOURCES_DIR@")

include(FindPackageHandleStandardArgs)
set(_dummy_var "found")
find_package_handle_standard_args(@PROJECT_NAME@ DEFAULT_MSG _dummy_var)
